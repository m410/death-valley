
plugins {
    id 'java-library'
    id 'maven-publish'
    id 'idea'
}


version = '1.0.0-SNAPSHOT'
group = 'us.m410.deathvalley'

sourceCompatibility = 1.8
defaultTasks 'build'
description 'Death Valley'


repositories {
    mavenCentral()
}

dependencies {
    compile('org.apache.commons:commons-lang3:3.7')
    compile('javax.validation:validation-api:2.0.1.Final')

    testCompile('javax.annotation:javax.annotation-api:1.3.2')
    testCompile('org.hibernate.common:hibernate-commons-annotations:5.0.4.Final')
    testCompile('org.hibernate.javax.persistence:hibernate-jpa-2.1-api:1.0.2.Final')
    testCompile("org.junit.jupiter:junit-jupiter-api:5.2.0")
    testRuntime("org.junit.jupiter:junit-jupiter-engine:5.2.0")
}

test {
    jvmArgs '--illegal-access=deny', '--add-opens','java.base/java.lang=ALL-UNNAMED'
    useJUnitPlatform {
        excludeEngines 'junit-vintage'
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'death-valley'
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'Death Valley'
                description = 'Java server-side counterpart to ng-death-valley angular library to perform form validation.'
                url = 'https://gitlab.com/m410/death-valley'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'michaelfortin'
                        name = 'Michael Fortin'
                        email = 'michael410@pm.me'
                    }
                }
                scm {
                    url = 'git@gitlab.com:m410/death-valley.git'
                }
            }
        }
    }
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html4', true)
    }
}

artifacts {
    archives sourcesJar
    archives javadocJar
}